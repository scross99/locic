# Compiler tests for programs that are expected to
# NOT compile and produce a specific error.

macro(test_check_error name)
	cmake_parse_arguments(TEST_CHECK_ERROR "EXPECT_FAIL;USE_OLD_PARSER" "" "" ${ARGN})
	if(TEST_CHECK_ERROR_USE_OLD_PARSER)
		message(WARNING "Test '${name}' is using old parser.")
		set(USE_NEW_PARSER_OPT "--use-old-parser")
	else()
		set(USE_NEW_PARSER_OPT "")
	endif()
	set(test_name "TestCheckError${name}")
	add_test(NAME "${test_name}"
		COMMAND ${CMAKE_BINARY_DIR}/tools/loci-test --test-name "${test_name}" --expected-error=${CMAKE_CURRENT_SOURCE_DIR}/${name}.log ${CMAKE_CURRENT_SOURCE_DIR}/${name}.loci ${USE_NEW_PARSER_OPT})
	if(TEST_CHECK_ERROR_EXPECT_FAIL)
		set_tests_properties("${test_name}" PROPERTIES LABELS "EXPECT_FAIL")
	endif()
endmacro(test_check_error)

add_subdirectory(Alias)
add_subdirectory(Cast)
add_subdirectory(Class)
add_subdirectory(Conditional)
add_subdirectory(Const)
add_subdirectory(Datatype)
add_subdirectory(Defaults)
add_subdirectory(Exception)
add_subdirectory(LifetimeMethods)
add_subdirectory(Loop)
add_subdirectory(Move)
add_subdirectory(NameRef)
add_subdirectory(Primitives)
add_subdirectory(ScopeExit)
add_subdirectory(SizeOf)
add_subdirectory(StaticRef)
add_subdirectory(Struct)
add_subdirectory(Syntax)
add_subdirectory(Templates)
add_subdirectory(Unused)

