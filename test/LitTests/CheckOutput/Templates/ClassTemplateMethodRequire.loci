// RUN: %locic %s --interpret > %t
// RUN: FileCheck < %t %s

// CHECK: PrintFirstAble.printFirst(): value = 2500
// CHECK: PrintBothAble.printFirst(): first = 2600
// CHECK: PrintBothAble.printSecond(): second = 2700

import void printf(const ubyte * str, ...);

interface FirstInterface {
	void printFirst() const;
}

interface SecondInterface {
	void printSecond() const;
}

template <typename T>
require(T : FirstInterface and is_movable<T>)
class TestClass(T value) {
	static Create(T value) {
		return @(move value);
	}
	
	void indirectPrintFirst() {
		@value.printFirst();
	}
	
	void indirectPrintSecond() require(T : SecondInterface) {
		@value.printSecond();
	}
}

class PrintFirstAble(int value) {
	static create = default;
	
	void printFirst() const {
		printf(C"PrintFirstAble.printFirst(): value = %d\n", @value);
	}
}

class PrintBothAble(int first, int second) {
	static create = default;
	
	void printFirst() const {
		printf(C"PrintBothAble.printFirst(): first = %d\n", @first);
	}
	
	void printSecond() const {
		printf(C"PrintBothAble.printSecond(): second = %d\n", @second);
	}
}

export int main(unused int argc, unused ubyte ** argv) {
	TestClass<PrintFirstAble> printFirstAble = TestClass<PrintFirstAble>(PrintFirstAble(2500));
	printFirstAble.indirectPrintFirst();
	
	TestClass<PrintBothAble> printBothAble = TestClass<PrintBothAble>(PrintBothAble(2600, 2700));
	printBothAble.indirectPrintFirst();
	printBothAble.indirectPrintSecond();
	return 0;
}

