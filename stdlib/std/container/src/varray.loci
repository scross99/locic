export std.container 0.1.0 {
	
	namespace std {
	
		/**
		 * \brief Resizable array.
		 */
		template <sized_type T>
		class varray(size_t size, size_t capacity, T* data) {
			static create() noexcept {
				return @(0u, 0u, null);
			}
			
			~ {
				for (auto i: reverse_range<size_t>(@size, 0)) {
					nolval(@data[i - 1u]).destroyvalue();
				}
				
				release(reinterpret_cast<T*, void*>(@data));
			}
			
			// Prevent the creation of a liveness indicator.
			void __setdead() noexcept {
				@data = null;
			}
			
			bool __islive() const noexcept {
				return @data != null;
			}
			
			void reserve(size_t size) require (movable<T>) {
				if (size <= self.capacity()) {
					return;
				}
				
				final T* newData = reinterpret_cast<void*, T*>(allocate(size * sizeof(T)));
				
				for (auto i: counter<size_t>(0u, @size, 1u)) {
					nolval(newData[i]).setvalue(nolval(@data[i]).extractvalue());
				}
				
				release(reinterpret_cast<T*, void*>(@data));
				
				@data = newData;
				@capacity = size;
			}
			
			size_t capacity() const noexcept {
				return @capacity;
			}
			
			void resize(size_t size, const T element) require(copyable<T>) {
				self.reserve(size);
				if (size < self.size()) {
					for (auto i: reverse_counter<size_t>(self.size(), size, 1u)) {
						nolval(@data[i]).destroyvalue();
					}
				} else {
					for (auto i: counter<size_t>(self.size(), size, 1u)) {
						nolval(@data[i]).setvalue(element.copy());
					}
				}
				@size = size;
			}
			
			bool empty() const noexcept {
				return @size == 0u;
			}
			
			template <bool IsConst>
			const<IsConst>(T)* data() const(IsConst) noexcept {
				return @data;
			}
			
			size_t size() const noexcept {
				return @size;
			}
			
			template <bool IsConst>
			lval ptr_lval_t<const<IsConst>(T)> index(size_t index) const(IsConst) noexcept {
				assert index < @size;
				return @data[index];
			}
			
			void clear() noexcept {
				for (auto i: reverse_counter<size_t>(@size, 0u, 1u)) {
					nolval(@data[i]).destroyvalue();
				}
				@size = 0u;
			}
			
			void push_back(T value) require (movable<T>) {
				self.reserve(@size + 1u);
				nolval(@data[@size]).setvalue(move value);
				@size = @size + 1u;
			}
			
			void pop_back() noexcept {
				assert !self.empty();
				@size = @size - 1u;
				nolval(@data[@size]).destroyvalue();
			}
			
			varray<notag(T)> copy() const require(copyable<T>) {
				auto array = varray<T>();
				array.reserve(@size);
				for (auto i: counter<size_t>(0u, @size, 1u)) {
					array.push_back(self[i].copy());
				}
				return move array;
			}
			
			template <bool IsConst>
			varray_range<T, IsConst> all() const(IsConst) noexcept {
				return varray_range<T, IsConst>(self);
			}
			
		}
		
		template <sized_type T, bool IsArrayConst>
		class varray_range(const<IsArrayConst>(varray<T>)& array, size_t begin, size_t end) {
			static create(const<IsArrayConst>(varray<T>)& array) noexcept {
				return @(array, 0u, array.size());
			}
			
			implicitCopy = default;
			
			bool empty() const noexcept {
				return @begin == @end;
			}
			
			void skip_front() noexcept {
				assert !self.empty();
				@begin += 1u;
			}
			
			template <bool IsConst>
			lval ptr_lval_t<const<IsConst or IsArrayConst>(T)> front() const(IsConst) noexcept {
				assert !self.empty();
				return @array[@begin];
			}
			
			void skip_back() noexcept {
				assert !self.empty();
				@end -= 1u;
			}
			
			template <bool IsConst>
			lval ptr_lval_t<const<IsConst or IsArrayConst>(T)> back() const(IsConst) noexcept {
				assert !self.empty();
				return @array[@end - 1u];
			}
		}
		
	}
	
}

